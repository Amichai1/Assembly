#include "msp430g2553.h"
#define  LCD         &P1OUT
#define  LcdControl  &P2OUT
Rigth  EQU   0x14
Left   EQU   0x10
;======================= Macro Fun =============================================

;------------------------- Debounce --------------------------------------------

Debounce    MACRO  cycles
            LOCAL  DeLoop
            mov.w  cycles,R4     
DeLoop      dec.w  R4                     
            jnz    DeLoop
            ENDM            

;------------------------- prep LCD command ------------------------------------

LCD_Macro   MACRO  command,delay,RS
            push.b command
            push.w delay
            push.w RS
            call   #Lcd_cmd
            ENDM

;------------------------- Movecursor ------------------------------------------

MovecursorF MACRO     DirectionF,TimesF
            LOCAL     MoveLoopF
            mov.w     TimesF,R12
MoveLoopF   LCD_Macro DirectionF,#1750,#0
            dec       R12
            jnz       MoveLoopF
            ENDM          

;------------------------- Convert to Decimal ----------------------------------

compDec   MACRO unit,unitnum
          LOCAL unitLoop,Nextunit
unitLoop  cmp.w unitnum,Hexnum
          jn    Nextunit
          inc.w unit
          sub.w unitnum,Hexnum
          jmp   unitLoop
Nextunit  ENDM

;========================= Functions Module ====================================
         
         MODULE Functions
         PUBLIC ConfSYS,ConfLCD,Lcd_cmd,LCDPrint
         PUBLIC freqLCD,FreqMesu
         EXTERN cyclenum,cyclenum1,cyclenum2
         EXTERN FreqShow,RigthChar,LeftChar,Frequency1,Frequency2,FreqNow
         EXTERN Freq1,Freq2,Freq3,Freq4,Freq5,Freq6,Freq7,Freq8,Freq9,Freq10
         EXTERN Freq11,Freq12,Freq13,Freq14,Freq15,FreqShow,Freqflag
         EXTERN thousands10,thousands,Hundreds,Dozens,unity,Hexnum
         RSEG   CODE        

;------------------------ Ports Config Routine ---------------------------------

ConfSYS  bic.b #0xff,&P1SEL    ;All Port1 is I/O Mode
         bic.b #0xeb,&P2SEL    ;All Port2 is I/O Mode (exepte p2.2,4)
         bis.b #0x14,&P2SEL    ;p2.4 is CCI2A Mode, p2.2 is TA1 PWM output mode
         bis.b #0xff,&P1DIR    ;All Port1 Pin Are Output
         bic.b #0x1b,&P2DIR    ;P2.1-0,3-4 Pin Are input
         bis.b #0xe4,&P2DIR    ;P2.2,5-7 Pin Are Output
         bic.b #0x03,P2IES     ;setup P2.0-1 to be falling edge (pull-up)
         bis.b #0x08,P2IES     ;setup P2.3 to be risin edge (pull-down)
         bis.b #0x0b,P2IE      ;enable interupt from P2.0,1,3
         bic.b #0xff,&P2IFG    ;Clear interupt Flags
         bic.b #0xff,&P1OUT
         bic.b #0xff,&P2OUT       
         RET

;------------------------- LCD Config Routine ----------------------------------

ConfLCD  LCD_Macro #0x3f,#5250,#0   ;#command/ascii,#delay,#RS Bit
         LCD_Macro #0x3f,#1750,#0
         LCD_Macro #0x3f,#70,#0
         LCD_Macro #0x3c,#1750,#0   ;define 16x2 LCD
         LCD_Macro #0x0f,#1750,#0   ;LCD cursor on
         LCD_Macro #0x01,#1750,#0   ;Clear the screen
         LCD_Macro #0x06,#1750,#0   ;increment cursor
         LCD_Macro #0x80,#1750,#0   
         LCD_Macro #0x02,#1750,#0   ;cursor to the head of 1st line
         RET

;------------------------ LCD Command execute Routine --------------------------

Lcd_cmd  pop.w  R6                ;PC for RET
         pop.w  R7                ;RS-command/ascii code?
         pop.w  R4                ;delay time
         pop.b  R5                ;command/ascii data
         mov.b  #0,LCD            ;clear LCD
         Debounce R4
         tst    R7                ;check if its command/ascii code
         jz     command
         bis.b  #0x40,LcdControl  ;if it is ascii, set RS bit
command  mov.b  R5,LCD         
         bis.b  #0x20,LcdControl  ;E=1, execute the command
         nop
         nop
         bic.b  #0x20,LcdControl  ;E=0
         push.w R6
         bic.b  #0x40,LcdControl  ;clear RS Bit
         RET

;------------------------ LCD string print Routine -----------------------------

LCDPrint  pop.w     R8
          pop.w     R9        
printing  LCD_Macro 0(R9),#1750,#1
          inc.w     R9
          tst.b     0(R9)
          jnz       printing  
          push.w    R8
          RET         
          
;---------------------- print frequency on LCD Routine -------------------------

freqLCD   mov.w FreqShow,Hexnum
          mov.w #48,thousands10
          mov.w #48,thousands
          mov.w #48,Hundreds
          mov.w #48,Dozens
          mov.w #48,unity
          compDec thousands10,#10000
          compDec thousands,#1000
          compDec Hundreds,#100
          compDec Dozens,#10
          compDec unity,#1
          
          MovecursorF #Left,#5
          LCD_Macro  thousands10,#1750,#1
          LCD_Macro  thousands,#1750,#1
          LCD_Macro  Hundreds,#1750,#1
          LCD_Macro  Dozens,#1750,#1
          LCD_Macro  unity,#1750,#1
          
          RET

;---------------------- frequency masure ---------------------------------------

FreqMesu  cmp.b #1,Freqflag
          jnz   second       
          mov.w TA1CCR2,cyclenum1
          mov.w cyclenum1,R15  
          sub.w cyclenum2,R15
          mov.w R15,cyclenum
          call  #stablefreq
           
          mov.b #0,Freqflag
          RET
                   
second    mov.w TA1CCR2,cyclenum2
          mov.w cyclenum2,R15
          sub.w cyclenum1,R15
          mov.w R15,cyclenum
          mov.b #1,Freqflag
          RET 

;---------------------- frequency noise clean ----------------------------------

stablefreq mov.w #0x00,FreqNow           
           mov.w #32100,R14
DivLoop    sub.w R15,R14
           cmp.w R15,R14
           jn    stable
           tst.w R15
           jz    stable
           inc.w FreqNow
           jmp   DivLoop
          
stable    rra.w FreqNow
          rra.w FreqNow
          rra.w FreqNow
          rra.w FreqNow
          mov.w Freq15,R12
          mov.w Freq14,Freq15
          mov.w Freq13,Freq14
          mov.w Freq12,Freq13
          mov.w Freq11,Freq12
          mov.w Freq10,Freq11
          mov.w Freq9,Freq10
          mov.w Freq8,Freq9
          mov.w Freq7,Freq8
          mov.w Freq6,Freq7
          mov.w Freq5,Freq6
          mov.w Freq4,Freq5
          mov.w Freq3,Freq4
          mov.w Freq2,Freq3
          mov.w Freq1,Freq2
          mov.w FreqNow,Freq1           
          add.w Freq15,R12
          add.w Freq14,R12
          add.w Freq13,R12
          add.w Freq12,R12
          add.w Freq11,R12
          add.w Freq10,R12
          add.w Freq9,R12
          add.w Freq8,R12
          add.w Freq7,R12
          add.w Freq6,R12
          add.w Freq5,R12
          add.w Freq4,R12
          add.w Freq3,R12
          add.w Freq2,R12
          add.w Freq1,R12
          mov.w R12,FreqShow
          call  #freqLCD
          RET

          ENDMOD
;-------------------------------------------------------------------------------
           
          END